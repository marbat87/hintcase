apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

group = 'com.joanfuentes.hintcaseassets' // Change this to match your package name
version = '1.0.0' // Change this to match your version number

def globalConfiguration = rootProject.extensions.getByName("ext")

android {
    compileSdkVersion globalConfiguration.getAt("androidCompileSdkVersion")
    buildToolsVersion globalConfiguration.getAt("androidBuildToolsVersion")

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion globalConfiguration.getAt("androidTargetSdkVersion")
        versionCode globalConfiguration.getAt("androidVersionCodeAssetsLibrary")
        versionName globalConfiguration.getAt("androidVersionNameAssetsLibrary")
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }
}

dependencies {
    def libraryDependencies = globalConfiguration.assetsLibraryDependencies
    compile project(':HintCase')
    compile libraryDependencies.AppCompat
}

task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

task generateJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath()
            .join(File.pathSeparator))
}

task generateJavadocsJar(type: Jar) {
    from generateJavadocs.destinationDir
    classifier 'javadoc'
}

generateJavadocsJar.dependsOn generateJavadocs

artifacts {
    archives generateJavadocsJar
    archives generateSourcesJar
}

bintray {
    user = 'marbat87'
    key = '151776ee00e86e62cda3351441ccac59e9c11243'
    pkg {
        repo = 'maven'
        name = 'HintCaseAssets' // the name of the package in BinTray

        version {
            name = 'HintCaseAssets' // the name of your library project
            desc = 'Initial Release'
            released  = new Date()
            vcsTag = '1.0.0' // the version
        }

        licenses = ['MIT']
        vcsUrl = 'https://github.com/marbat87/hintcase.git' // your GitHub repo
        websiteUrl = 'https://github.com/marbat87/hintcase.git' // your website or whatever has documentation
    }
    configurations = ['archives']
}
